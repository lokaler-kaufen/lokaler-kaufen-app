plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'idea'
    id "org.sonarqube" version "2.8"
    // Writes git.properties with commit hash, etc.
    id "com.gorylenko.gradle-git-properties" version "2.2.2"

    id "com.github.erdi.webdriver-binaries" version "2.2"
    id "com.github.erdi.idea-base" version "2.2"
}

group = 'de.qaware.mercury'
version = '1.1.0-SNAPSHOT'

sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }

    e2eTestCompile.extendsFrom testImplementation
    e2eTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

ext {
    drivers = ["firefox", "chrome", "chromeHeadless"]

    ext {
        gebVersion = '3.4'
        seleniumVersion = '3.14.0'
        chromeDriverVersion = '79.0.3945.36'
        geckoDriverVersion = '0.26.0'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.flywaydb:flyway-core'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    // Fixes the 'warning: unknown enum constant When.MAYBE' warnings
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // Swagger for API docs
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    // Bcrypt for password hashing
    implementation 'at.favre.lib:bcrypt:0.9.0'

    // Geodesy library for distance calculations
    implementation 'org.gavaghan:geodesy:1.1.3'

    // JWT for the auth tokens
    implementation 'com.auth0:java-jwt:3.10.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // mandatory dependencies for using Spock
    testImplementation "org.codehaus.groovy:groovy-all:2.5.8"
    testImplementation platform("org.spockframework:spock-bom:2.0-M1-groovy-2.5")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-spring"

    // Testcontainers for integration tests
    testCompile 'org.testcontainers:postgresql:1.13.0'

    testRuntimeOnly "net.bytebuddy:byte-buddy:1.9.3"
    testRuntimeOnly "org.objenesis:objenesis:2.6"

    e2eTestCompile "org.gebish:geb-spock:$gebVersion"
    e2eTestCompile "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    e2eTestCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
}

test {
    // Run gradle with -DskipIntegrationTests to skip integration tests
    if (System.getProperty("skipIntegrationTests") != null) {
        systemProperty("spock.configuration", "SpockNoIntegrationTests.groovy")
    }

    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "qaware_wir-vs-virus"
        property "sonar.organization", "qaware"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

gitProperties {
    // We read last commit and stuff from .git directory
    // This disables the exception if the .git folder isn't found
    failOnNoGitDirectory = false
}

bootJar {
    // Remove version from artifact
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

// have some fun ./gradlew kick ass
task kick(dependsOn: clean) {}

sourceSets {
    e2eTest {
        groovy {
            compileClasspath += sourceSets.main.output + configurations.e2eTestCompile
            runtimeClasspath += output + compileClasspath + configurations.e2eTestRuntime
            srcDir file('src/e2e/groovy')
        }
        resources.srcDir file('src/e2e/resources')
    }
}

idea {
    module {
        testSourceDirs += sourceSets.e2eTest.groovy.srcDirs
        testResourceDirs += sourceSets.e2eTest.resources.srcDirs
        scopes.TEST.plus += [ configurations.e2eTestCompile, configurations.testCompile ]
    }
}

webdriverBinaries {
    chromedriver {
        version = chromeDriverVersion
        fallbackTo32Bit = true
    }
    geckodriver {
        version = geckoDriverVersion
    }
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        useJUnitPlatform()

        testClassesDirs = sourceSets.e2eTest.output
        classpath = sourceSets.e2eTest.runtimeClasspath

        testLogging {
            events "passed", "skipped", "failed"
        }

        reports {
            html.enabled = true
        }

        outputs.upToDateWhen { false }

        systemProperty "geb.build.baseUrl", System.getProperty("baseUrl", "http://localhost:4200")
        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
    }
}